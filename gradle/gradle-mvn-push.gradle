apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// load properties
Properties properties = new Properties()
File localPropertiesFile = rootProject.file("local.properties");
if(localPropertiesFile.exists()){
    properties.load(localPropertiesFile.newDataInputStream())
}

def developerId = properties.getProperty("POM_DEVELOPER_ID")
def developerName = properties.getProperty("POM_DEVELOPER_NAME")
def developerEmail = properties.getProperty("POM_DEVELOPER_EMAIL")

def bintrayUser = properties.getProperty("BINTRAY_USER")
def bintrayApikey = properties.getProperty("BINTRAY_APIKEY")

group = GROUP

// This generates POM.xml with proper parameters
install {
    repositories.mavenInstaller {
        pom {
            project {
                name PROJECT_NAME
                groupId GROUP
                artifactId POM_ARTIFACT_ID
                version VERSION_NAME
                packaging POM_PACKAGING
                description POM_DESCRIPTION
                url PROJECT_URL
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection GIT_URL
                    developerConnection WEB_URL
                    url PROJECT_URL
                }
            }
        }
    }

}

// bintray configuration
bintray {
    user = bintrayUser
    key = bintrayApikey
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = PROJECT_NAME
        version VERSION_NAME
        websiteUrl = WEB_URL
        vcsUrl = GIT_URL
        licenses = ["Apache-2.0"]
        publish = true
    }
}

sourceDocFiles.each {
    apply from: it
}